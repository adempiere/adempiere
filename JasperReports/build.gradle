plugins {
	id 'java-library'
	id 'maven-publish'
	id 'signing'
}


repositories {
    mavenLocal()
    mavenCentral()
}

allprojects {
	tasks.withType(Javadoc) {
    	options.addStringOption('Xdoclint:none', '-quiet')
    }
}

dependencies {
    // https://mvnrepository.com/artifact/net.sf.jasperreports/jasperreports
    api 'net.sf.jasperreports:jasperreports:6.17.0'
    // https://mvnrepository.com/artifact/com.lowagie/itext
    api 'com.lowagie:itext:2.1.7'
    api 'net.sourceforge.barbecue:barbecue:1.5-beta1'

	// Local
	api project(path: ':base')
}

sourceSets {
	main {
		java {
			srcDirs = ['src']
		}
	}
}

java {
    withJavadocJar()
    withSourcesJar()
}


def entityType = 'D'
version = System.getenv("ADEMPIERE_LIBRARY_VERSION") ?: "local-1.0.0"

jar {
	manifest {
		attributes(
			"Implementation-Title": "Adempiere JasperReports Management",
			"Implementation-Version": version,
			"EntityType": entityType
		)
	}
}

publishing {
    repositories {
		mavenLocal()

        maven {
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = findProperty("sonatypeUsername")
                password = findProperty("sonatypePassword")
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
        	groupId 'io.github.adempiere'
            artifactId 'jasperreports'
            version
           	from components.java
           	pom {
                name = 'JasperReports'
                description = 'JasperReports, a implementation of JasperReports for ADempiere.'
                url = 'http://adempiere.io/'
                licenses {
                    license {
                        name = 'GNU General Public License, version 2'
                        url = 'https://www.gnu.org/licenses/old-licenses/gpl-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'yamelsenih'
                        name = 'Yamel Senih'
                        email = 'ysenih@erpya.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/adempiere/adempiere.git'
                    developerConnection = 'scm:git:ssh://github.com/adempiere/adempiere.git'
                    url = 'http://github.com/adempiere/adempiere'
                }
            }
		}
	}
}


task cleanBuildPublishLocal(type: GradleBuild) {
	tasks = ['clean', 'build', 'publishToMavenLocal']
}


signing {
	def isReleaseVersion = !version.toString().startsWith("local") && !version.toString().endsWith("-SNAPSHOT")

	sign configurations.archives

	setRequired {
		// signing is required if this is a release version and the artifacts are to be published
		// do not use hasTask() as this require realization of the tasks that maybe are not necessary
		(isReleaseVersion || version.toString().equals("build")) && gradle.taskGraph.allTasks.any {
			it.equals(PublishToMavenRepository)
		}
	}

	def signingKey = findProperty("signingKey")
	def signingPassword = findProperty("signingPassword")
	useInMemoryPgpKeys(signingKey, signingPassword)
	sign publishing.publications.mavenJava
}
