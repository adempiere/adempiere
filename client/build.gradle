plugins {
	id 'java-library'
	id 'maven-publish'
	id 'signing'
}


repositories {
    mavenLocal()
    mavenCentral()
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

dependencies {
    implementation fileTree(
		dir: '../tools/lib',
		include: [
			'swingx-1.6.jar',
			'jnlp.jar',
			'icepdf-viewer-6.2.2.jar',
			'lablib-checkboxtree-3.3.jar',
			'groovy-all-2.4.15.jar',
			'harness-1.0.0.jar',
			'oracle.jar',
			'postgresql.jar'
		]
	)

    // https://mvnrepository.com/artifact/com.itextpdf/itextpdf
    implementation group: 'com.itextpdf', name: 'itextpdf', version: '5.5.2'
    // https://mvnrepository.com/artifact/com.jgoodies/looks
	implementation group: 'com.jgoodies', name: 'looks', version: '2.0.4'
    // https://mvnrepository.com/artifact/org.jfree/jfreechart
	implementation group: 'org.jfree', name: 'jfreechart', version: '1.0.14'
    // https://mvnrepository.com/artifact/io.vavr/vavr
    implementation group: 'io.vavr', name: 'vavr', version: '0.10.4'
    // https://mvnrepository.com/artifact/com.miglayout/miglayout
    implementation group: 'com.miglayout', name: 'miglayout', version: '3.7.1'
    // https://mvnrepository.com/artifact/org.beanshell/bsh
    implementation group: 'org.beanshell', name: 'bsh', version: '2.0b5'

	// Local
	api project(path: ':base')
	api project(path: ':org.adempiere.request')
	api project(path: ':JasperReports')
}

sourceSets {
	main {
		java {
			srcDirs = ['src']
		}
	}
}

java {
    withJavadocJar()
    withSourcesJar()
}


def entityType = 'D'
version = System.getenv("ADEMPIERE_LIBRARY_VERSION") ?: "local-1.0.0"

jar {
	manifest {
		attributes(
			"Implementation-Title": "Adempiere Swing Client",
			"Implementation-Version": version,
			"EntityType": entityType
		)
	}
}

publishing {
    repositories {
        maven {
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
				username = findProperty("sonatypeUsername")
				password = findProperty("sonatypePassword")
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
        	groupId 'io.github.adempiere'
            artifactId 'swing'
            version
           	from components.java
           	pom {
                name = 'Swing'
                description = 'Swing UI and related libraries.'
                url = 'http://adempiere.io/'
                licenses {
                    license {
                        name = 'GNU General Public License, version 2'
                        url = 'https://www.gnu.org/licenses/old-licenses/gpl-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'yamelsenih'
                        name = 'Yamel Senih'
                        email = 'ysenih@erpya.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/adempiere/adempiere.git'
                    developerConnection = 'scm:git:ssh://github.com/adempiere/adempiere.git'
                    url = 'http://github.com/adempiere/adempiere'
                }
            }
		}
	}
}


task cleanBuildPublishLocal(type: GradleBuild) {
	tasks = ['clean', 'build', 'publishToMavenLocal']
}


signing {
	def isReleaseVersion = !version.toString().startsWith("local") && !version.toString().endsWith("-SNAPSHOT")

	sign configurations.archives

	setRequired {
		// signing is required if this is a release version and the artifacts are to be published
		// do not use hasTask() as this require realization of the tasks that maybe are not necessary
		(isReleaseVersion || version.toString().equals("build")) && gradle.taskGraph.allTasks.any {
			it.equals(PublishToMavenRepository)
		}
	}

	def signingKey = findProperty("signingKey")
	def signingPassword = findProperty("signingPassword")
	useInMemoryPgpKeys(signingKey, signingPassword)
	sign publishing.publications.mavenJava
}
