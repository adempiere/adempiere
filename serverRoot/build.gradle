plugins {
=	id 'java-library'
	id 'maven-publish'
	id 'signing'
}


repositories {
    mavenLocal()
    mavenCentral()
}

allprojects {
    tasks.withType(Javadoc) {
    	options.addStringOption('Xdoclint:none', '-quiet')
   	}
}

dependencies {
	// https://mvnrepository.com/artifact/com.sun.jndi.ldap/ldapbp
	// implementation group: 'com.sun.jndi.ldap', name: 'ldapbp', version: '1.0'
	// implementation group: 'com.sun.jndi', name: 'ldap', version: '1.2.4'

    // https://mvnrepository.com/artifact/javaee/javaee-api
	api 'javaee:javaee-api:5'
    // https://mvnrepository.com/artifact/org.apache.activemq/activemq-core
    api 'org.apache.activemq:activemq-core:5.7.0'
    // https://mvnrepository.com/artifact/it.sauronsoftware.cron4j/cron4j
    api 'it.sauronsoftware.cron4j:cron4j:2.2.5'

	// Local
	api project(path: ':org.adempiere.project')
}

sourceSets {
	main {
		java {
			srcDirs = ['src/main/server']
		}
	}
}

java {
    withJavadocJar()
    withSourcesJar()
}

compileJava {
	options.compilerArgs.add('--add-exports=java.base/jdk.internal.vm.annotation=ALL-UNNAMED')
	options.compilerArgs.add('--add-opens=java.base/jdk.internal.loader=ALL-UNNAMED')
	options.compilerArgs.add('--add-exports=java.naming/com.sun.jndi.ldap=ALL-UNNAMED')
}


def entityType = 'D'
version = System.getenv("ADEMPIERE_LIBRARY_VERSION") ?: "local-1.0.0"

jar {
	manifest {
		attributes(
			"Implementation-Title": "Adempiere Server Root",
			"Implementation-Version": version,
			"EntityType": entityType
		)
	}
}

publishing {
    repositories {
		mavenLocal()

        maven {
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
				username = findProperty("sonatypeUsername")
				password = findProperty("sonatypePassword")
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
        	groupId 'io.github.adempiere'
			artifactId 'server-root'
            version
           	from components.java
           	pom {
				name = 'Server Root'
				description = 'Server Root, and Deploy Server Files.'
                url = 'http://adempiere.io/'
                licenses {
                    license {
                        name = 'GNU General Public License, version 2'
                        url = 'https://www.gnu.org/licenses/old-licenses/gpl-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'yamelsenih'
                        name = 'Yamel Senih'
                        email = 'ysenih@erpya.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/adempiere/adempiere.git'
                    developerConnection = 'scm:git:ssh://github.com/adempiere/adempiere.git'
                    url = 'http://github.com/adempiere/adempiere'
                }
            }
		}
	}
}


task cleanBuildPublishLocal(type: GradleBuild) {
	tasks = ['clean', 'build', 'publishToMavenLocal']
}

signing {
	def isReleaseVersion = !version.toString().startsWith("local") && !version.toString().endsWith("-SNAPSHOT")

	sign configurations.archives

	setRequired {
		// signing is required if this is a release version and the artifacts are to be published
		// do not use hasTask() as this require realization of the tasks that maybe are not necessary
		(isReleaseVersion || version.toString().equals("build")) && gradle.taskGraph.allTasks.any {
			it.equals(PublishToMavenRepository)
		}
	}

	def signingKey = findProperty("signingKey")
	def signingPassword = findProperty("signingPassword")
	useInMemoryPgpKeys(signingKey, signingPassword)
	sign publishing.publications.mavenJava
}
