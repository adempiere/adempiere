plugins {
	id 'java-library'
	id 'maven-publish'
	id 'signing'
}


repositories {
    mavenLocal()
    mavenCentral()
}

allprojects {
	tasks.withType(Javadoc) {
    	options.addStringOption('Xdoclint:none', '-quiet')
    }
}

dependencies {
	implementation fileTree(
		dir: '../tools/lib/testing', 
		include: [
			'SuperCSV-with_src-1.52.jar'
		]
	)

	// Local
	api project(path: ':base')

    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-api
	// testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    api 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    // https://mvnrepository.com/artifact/org.mockito/mockito-junit-jupiter
    api 'org.mockito:mockito-junit-jupiter:3.6.0'
    // https://mvnrepository.com/artifact/org.junit.platform/junit-platform-console-standalone
    api 'org.junit.platform:junit-platform-console-standalone:1.7.0'
}

sourceSets {
	main {
		java {
			srcDirs = ['src']
		}
	}
}

java {
    withJavadocJar()
    withSourcesJar()
}


def entityType = 'D'
version = System.getenv("ADEMPIERE_LIBRARY_VERSION") ?: "local-1.0.0"

jar {
	manifest {
		attributes(
			"Implementation-Title": "Adempiere Test Module",
			"Implementation-Version": version,
			"EntityType": entityType
		)
	}
}

publishing {
    repositories {
		mavenLocal()

        maven {
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = findProperty("sonatypeUsername")
                password = findProperty("sonatypePassword")
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
        	groupId 'io.github.adempiere'
            artifactId 'adempiere.test'
            version
           	from components.java
           	pom {
                name = 'Adempiere Test'
				description = 'Test api dedicated to manage project.'
                url = 'http://adempiere.io/'
                licenses {
                    license {
                        name = 'GNU General Public License, version 2'
                        url = 'https://www.gnu.org/licenses/old-licenses/gpl-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'yamelsenih'
                        name = 'Yamel Senih'
                        email = 'ysenih@erpya.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/adempiere/adempiere.git'
                    developerConnection = 'scm:git:ssh://github.com/adempiere/adempiere.git'
                    url = 'http://github.com/adempiere/adempiere'
                }
            }
		}
	}
}


task cleanBuildPublishLocal(type: GradleBuild) {
	tasks = ['clean', 'build', 'publishToMavenLocal']
}

signing {
	def isReleaseVersion = !version.toString().startsWith("local") && !version.toString().endsWith("-SNAPSHOT")

	sign configurations.archives

	setRequired {
		// signing is required if this is a release version and the artifacts are to be published
		// do not use hasTask() as this require realization of the tasks that maybe are not necessary
		(isReleaseVersion || version.toString().equals("build")) && gradle.taskGraph.allTasks.any {
			it.equals(PublishToMavenRepository)
		}
	}

	def signingKey = findProperty("signingKey")
	def signingPassword = findProperty("signingPassword")
	useInMemoryPgpKeys(signingKey, signingPassword)
	sign publishing.publications.mavenJava
}
